plugins {
    id "com.jfrog.bintray" version '1.6'          // https://github.com/bintray/gradle-bintray-plugin
    id 'nu.studer.credentials' version '1.0.3'      // https://github.com/etiennestuder/gradle-credentials-plugin
    id 'com.github.kt3k.coveralls' version '2.6.3'  // https://github.com/kt3k/coveralls-gradle-plugin
    id 'com.github.hierynomus.license' version '0.11.0'  // https://github.com/hierynomus/license-gradle-plugin
    id "com.github.ben-manes.versions" version "0.13.0"
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'jacoco'
apply plugin: 'signing'

version = '1.0.5-SNAPSHOT'

ext.artifactGroupId = 'ch.petikoch.libs'
ext.description = 'A small java 7+ standalone library using a "task wait for graph" model to detect deadlocks'
ext.fileEncoding = 'UTF-8'
ext.pomFile = file("${project.buildDir}/generated-pom.xml")
ext."signing.keyId" = credentials.signingKeyId
ext."signing.password" = credentials.signingPassword
ext."signing.secretKeyRingFile" = credentials.signingSecretKeyRingFile

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    jcenter()
}

dependencies {
    testCompile 'org.codehaus.groovy:groovy-all:2.4.7'
    testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude group: 'org.codehaus.groovy'
    }
    testCompile 'com.google.guava:guava:20.0' // provides e.g. nice Multimap
}

jar {
    manifest {
        attributes 'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version'),
                'Built-Gradle': gradle.gradleVersion
    }
}

test {
    systemProperty 'file.encoding', project.fileEncoding
}

sourceSets.main.java.srcDirs = ['src/main/java']
sourceSets.test.java.srcDirs = []
sourceSets.main.groovy.srcDirs = []
sourceSets.test.groovy.srcDirs = ['src/test/groovy']

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

signing {
    sign configurations.archives
}

tasks.withType(GroovyCompile) {
    groovyOptions.encoding = project.fileEncoding
    options.encoding = project.fileEncoding
}

tasks.withType(JavaCompile) {
    options.encoding = project.fileEncoding
}

// http://forums.gradle.org/gradle/topics/set_maxparallelforks_to_number_of_cores_on_the_current_machine
tasks.withType(Test) {
    maxParallelForks = Runtime.getRuntime().availableProcessors()
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

def pomConfig = {
    name project.name
    description project.description
    url 'https://github.com/Petikoch/jtwfg'
    inceptionYear '2014'

    scm { url 'https://github.com/Petikoch/jtwfg.git' }

    developers {
        developer {
            id 'petikoch'
            name 'Peti Koch'
            email 'petikoch@gmail.com'
            url 'https://www.google.com/+PetiKoch'
        }
        developer {
            name 'Adrian Elsener'
        }
    }

/*    contributors {
        contributor {
            name 'John Doe'
        }
    }*/
}

publishing {
    publications {
        mavenCustom(MavenPublication) {
            groupId project.artifactGroupId
            artifactId project.name
            version project.version
            from components.java

            pom.withXml {

                asNode().children().last() + pomConfig

                def licencesNode = asNode().appendNode('licenses')
                def licenseNode = licencesNode.appendNode('license')
                licenseNode.appendNode('name', 'The Apache Software License, Version 2.0')
                licenseNode.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')
                licenseNode.appendNode('distribution', 'repo')

                // hack... build up the test dependencies ourselve
                // why? because maven-publish plugin creates crapy dependencies in pom.xml (gradle 2.2)
                // see http://stackoverflow.com/questions/20131915/publishing-artifact-from-gradle-project-to-bintray-maven-repository
                // solution: http://stackoverflow.com/questions/24743562/gradle-not-including-dependencies-in-published-pom-xml
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.testCompile.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                    dependencyNode.appendNode('scope', 'test')
                }
            }

            artifact sourceJar {
                classifier "sources"
            }

            //https://github.com/trustin/sphinx-gradle-plugin/blob/master/build.gradle
            // Sign the pom.xml
            pom.withXml {
                writeTo(project.ext.pomFile)
                def pomAscFile = signing.sign(project.ext.pomFile).signatureFiles[0]
                artifact(pomAscFile) {
                    classifier = null
                    extension = 'pom.asc'
                }
                //project.ext.pomFile.delete()
            }
            // Sign the artifacts.
            project.tasks.signArchives.signatureFiles.each {
                artifact(it) {
                    def matcher = it.file =~ /-(sources|javadoc)\.jar\.asc$/
                    if (matcher.find()) {
                        classifier = matcher.group(1)
                    } else {
                        classifier = null
                    }
                    extension = 'jar.asc'
                }
            }
        }
    }
}

model {
    tasks.generatePomFileForMavenCustomPublication {
        destination = project.ext.pomFile
    }
}

bintray {
    user = credentials.bintrayUserName       // https://github.com/etiennestuder/gradle-credentials-plugin
    key = credentials.bintrayApiKey          // https://github.com/etiennestuder/gradle-credentials-plugin
    publications = ['mavenCustom']   // see publications closure

    publish true

    pkg {
        repo = 'maven'
        name = project.name
        desc = project.description // doesn't work
        websiteUrl = 'https://github.com/Petikoch/jtwfg'
        issueTrackerUrl = 'https://github.com/Petikoch/jtwfg/issues'
        vcsUrl = 'https://github.com/Petikoch/jtwfg.git'
        licenses = ['Apache-2.0']
        labels = ['java']
        publicDownloadNumbers = true
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

license {
    header = rootProject.file('config/HEADER')
    strictCheck = true
}